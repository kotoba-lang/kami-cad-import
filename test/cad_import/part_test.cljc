(ns cad-import.part-test
  "Ported 1:1 from `src/part.rs`'s `#[cfg(test)] mod tests` in the
   original kami-cad-import crate (kotoba-lang/kami-engine, deleted PR
   #82). ADR-2607010930."
  (:require [clojure.test :refer [deftest is]]
            [cad-import.part :as part]))

(defn- provenance [sha]
  {:uri "scad://test" :sha256 sha :license "MIT"})

(defn- synth-part [id kind]
  {:id id :display-name id :kind kind :material :steel-mild
   :aabb-min [0.0 0.0 0.0] :aabb-max [1.0 0.5 0.3]
   :mass-kg nil :parent nil :break-group nil
   :source (provenance (apply str (repeat 64 "a")))
   :supplier part/default-supplier :revision "1.0.0"})

(deftest density-volume-to-mass
  (let [p (synth-part "rail" :chassis)
        m (part/effective-mass-kg p)]
    ;; 1.0 x 0.5 x 0.3 m^3 x 7850 kg/m^3 = 1177.5 kg
    (is (< (Math/abs (- m 1177.5)) 1e-2) (str "got " m))))

(deftest break-group-inherits-kind
  (let [p (synth-part "hood" :body)]
    (is (= (part/effective-break-group p) (part/default-break-group :body)))
    (let [p2 (assoc (synth-part "strut" :suspension) :break-group 99)]
      (is (= (part/effective-break-group p2) 99)))))

(deftest validate-rejects-missing-provenance
  (let [a (part/new-assembly "v1" {:uri "x" :sha256 "z" :license "MIT"})
        bad (assoc-in (synth-part "rail" :chassis) [:source :sha256] "")
        a (part/add-part a bad)
        [status err] (part/validate a)]
    (is (= status :error))
    (is (= (:error err) :missing-provenance))
    (is (= (:id err) "rail"))))

(deftest validate-rejects-unknown-parent
  (let [a (part/new-assembly "v1" (provenance (apply str (repeat 64 "a"))))
        child (assoc (synth-part "door" :body) :parent "nope")
        a (part/add-part a child)
        [status err] (part/validate a)]
    (is (= status :error))
    (is (= (:error err) :unknown-parent))
    (is (= (:child err) "door"))
    (is (= (:parent err) "nope"))))

(deftest validate-rejects-dangling-hardpoint
  (let [a (-> (part/new-assembly "v1" (provenance (apply str (repeat 64 "a"))))
              (part/add-part (synth-part "rail" :chassis))
              (part/add-hardpoint {:id "hp1" :from-part "rail" :to-part "ghost"
                                    :position [0.0 0.0 0.0] :kind :bolt}))
        [status err] (part/validate a)]
    (is (= status :error))
    (is (= (:error err) :unknown-hardpoint-part))
    (is (= (:part err) "ghost"))))

(deftest parts-by-break-group-aggregates
  (let [a (-> (part/new-assembly "v1" (provenance (apply str (repeat 64 "a"))))
              (part/add-part (synth-part "rail" :chassis))   ; group 1
              (part/add-part (synth-part "door" :body))      ; group 2
              (part/add-part (synth-part "hood" :body)))     ; group 2
        groups (part/parts-by-break-group a)]
    (is (= (count groups) 2))
    (is (= (first (nth groups 0)) 1))
    (is (= (count (second (nth groups 0))) 1))
    (is (= (first (nth groups 1)) 2))
    (is (= (count (second (nth groups 1))) 2))))
